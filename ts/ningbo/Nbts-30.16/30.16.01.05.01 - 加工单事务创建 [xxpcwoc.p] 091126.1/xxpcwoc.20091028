/* glcalrp.p - GENERAL LEDGER CALENDAR REPORT                           */
/* Copyright 1986-2004 QAD Inc., Carpinteria, CA, USA.                  */
/* All rights reserved worldwide.  This is an unpublished work.         */
/* $Revision: 1.9 $                                                     */
/*V8:ConvertMode=FullGUIReport                                          */
/* REVISION: 1.0      LAST MODIFIED: 09/18/86   BY: JMS                 */
/*                                   01/29/88   by: jms                 */
/*                                   02/24/88   by: jms                 */
/* REVISION: 4.0      LAST MODIFIED: 02/29/88   BY: WUG  *A175*         */
/* REVISION: 5.0      LAST MODIFIED: 06/22/89   BY: JMS  *B066*         */
/* REVISION: 6.0      LAST MODIFIED: 07/03/90   by: jms  *D034*         */
/* REVISION: 7.0      LAST MODIFIED: 10/04/91   by: jms  *F058*         */
/* REVISION: 7.4      LAST MODIFIED: 07/20/93   by: pcd  *H040*         */
/*                                   09/03/94   by: srk  *FQ80*         */
/* REVISION: 8.6      LAST MODIFIED: 10/11/97   BY: ays  *K0TT*         */
/* REVISION: 9.1      LAST MODIFIED: 03/24/00   BY: *N08T* Annasaheb Rahane */
/* REVISION: 9.1      LAST MODIFIED: 08/14/00   BY: *N0L1* Mark Brown       */
/* Old ECO marker removed, but no ECO header exists *F0PN*                  */
/* Revision: 1.7  BY: Katie Hilbert DATE: 08/03/01 ECO: *P01C* */
/* $Revision: 1.9 $ BY: Paul Donnelly (SB) DATE: 06/26/03 ECO: *Q00D* */
/*-Revision end---------------------------------------------------------------*/

/******************************************************************************/
/* All patch markers and commented out code have been removed from the source */
/* code below. For all future modifications to this file, any code which is   */
/* no longer required should be deleted and no in-line patch markers should   */
/* be added.  The ECO marker should only be included in the Revision History. */
/******************************************************************************/

/* SS - 090724.1 By: Bill Jiang */
/* SS - 090807.1 By: Bill Jiang */
/* SS - 091028.1 By: Bill Jiang */

/* SS - 091028.1 - RNB
[091028.1]

增加了车间事务

删除了数量为0的记录

[091028.1]

SS - 091028.1 - RNE */

/* SS - 090807.1 - RNB
[090807.1]

增加了累计加工单结算的处理

增加了是否允许出错的输入条件

[090807.1]

SS - 090807.1 - RNE */

/* SS - 090724.1 - RNB
[090724.1]

1.[期初数量] + [期间增加数量] = [期间入库(报废)数量] + [期末数量]
2.IF 结算 THEN [期末数量] = 0
3.ELSE IF 标准加工单 THEN [期间增加数量] = [工单数量] - [期初数量]"
4.ELSE IF [期间入库(报废)数量] <> 0 THEN [期末数量] = 0 ELSE [期末数量] = 1

错误:总账日历没维护,总账日历已结,上期未结,本期已结!

警告:记录已经存在,是否继续?

[090724.1]

SS - 090724.1 - RNE */

/* DISPLAY TITLE */
{mfdtitle.i "091028.1"}

define variable per like glc_per.
define variable yr like glc_year.
define variable entity like glcd_entity.
/* SS - 091028.1 - B */
DEFINE VARIABLE allow_errors LIKE mfc_logical INITIAL NO.
/* SS - 091028.1 - E */

DEFINE VARIABLE date1 AS DATE.
define variable l_yn        like mfc_logical             no-undo.

define variable efdate like tr_effdate.
define variable efdate1 like tr_date.

{glcabmeg.i} /* module closing engine include. */

/* SELECT FORM */
form
   entity colon 20    
   en_name NO-LABEL
   yr     colon 20    
   per     colon 20    
   skip
   /* SS - 091028.1 - B */
   SKIP (1)
   allow_errors COLON 20
   /* SS - 091028.1 - E */
with frame a side-labels attr-space width 80.

/* SET EXTERNAL LABELS */
setFrameLabels(frame a:handle).

date1 = DATE(MONTH(TODAY),1,YEAR(TODAY)) - 1.
FIND FIRST glc_cal 
   WHERE glc_domain = GLOBAL_domain
   AND glc_start <= date1
   AND glc_end >= date1
   NO-LOCK NO-ERROR.
IF AVAILABLE glc_cal THEN DO:
   yr = glc_year.
   per = glc_per.
END.

{wbrp01.i}

/* REPORT BLOCK */
mainloop:
repeat:
   if c-application-mode <> 'web' then
      update
         entity 
         yr 
         per
         /* SS - 091028.1 - B */
         allow_errors
         /* SS - 091028.1 - E */
      with frame a.

   {wbrp06.i &command = update &fields = "  entity yr per
      /* SS - 091028.1 - B */
      allow_errors
      /* SS - 091028.1 - E */
      " &frm = "a"}
      
   FIND FIRST en_mstr 
      WHERE en_domain = GLOBAL_domain
      AND en_entity = entity
      NO-LOCK NO-ERROR.
   IF AVAILABLE en_mstr THEN DO:
      DISPLAY
         en_name
         WITH FRAME a.
   END.
   ELSE DO:
      DISPLAY
         "" @ en_name
         WITH FRAME a.
   END.

   {gprun.i ""xxpcprhcv.p"" "(
      INPUT entity,
      INPUT yr,
      INPUT per,
      INPUT-OUTPUT efdate,
      INPUT-OUTPUT efdate1
      )"}
   IF RETURN-VALUE = "no" THEN DO:
      UNDO,RETRY.
   END.

   /* 已经创建 */
   FOR EACH en_mstr NO-LOCK
      WHERE en_domain = GLOBAL_domain
      AND en_entity = entity
      ,EACH si_mstr NO-LOCK
      WHERE si_domain = GLOBAL_domain
      AND si_entity = en_entity
      ,EACH xxpcwo_mstr NO-LOCK
      WHERE xxpcwo_domain = GLOBAL_domain
      AND xxpcwo_site = si_site
      AND xxpcwo_year = yr
      AND xxpcwo_per = per
      :
      /* 301606 - 记录已经存在.是否继续? */
      {pxmsg.i &MSGNUM=301606 &ERRORLEVEL=2 &CONFIRM=l_yn}

      if not l_yn then do:
         next-prompt per.
         UNDO mainloop, retry.
      end. /* IF NOT l_yn */

      LEAVE.
   END.

   if (c-application-mode <> 'web') or
      (c-application-mode = 'web' and
      (c-web-request begins 'data')) then do:

      /* CREATE BATCH INPUT STRING */
      bcdparm = "".
      {mfquoter.i entity }
      {mfquoter.i yr     }
      {mfquoter.i per     }
      /* SS - 091028.1 - B */
      {mfquoter.i allow_errors     }
      /* SS - 091028.1 - E */
   end.

   {xxSoftspeedLic.i "SoftspeedPC.lic" "SoftspeedPC"}

   /* OUTPUT DESTINATION SELECTION */
   {gpselout.i &printType = "printer"
               &printWidth = 80
               &pagedFlag = " "
               &stream = " "
               &appendToFile = " "
               &streamedOutputToTerminal = " "
               &withBatchOption = "yes"
               &displayStatementType = 1
               &withCancelMessage = "yes"
               &pageBottomMargin = 6
               &withEmail = "yes"
               &withWinprint = "yes"
               &defineVariables = "yes"}

   DO TRANSACTION ON STOP UNDO:
      /* 清除已有的数据 */
      FOR EACH si_mstr NO-LOCK
         WHERE si_domain = GLOBAL_domain
         AND si_entity = entity
         ,EACH xxpcwo_mstr EXCLUSIVE-LOCK
         WHERE xxpcwo_domain = GLOBAL_domain
         AND xxpcwo_site = si_site
         AND xxpcwo_year = yr
         AND xxpcwo_per = per
         :
         DELETE xxpcwo_mstr.
      END.

      /* 期初 */
      FOR EACH si_mstr NO-LOCK
         WHERE si_domain = GLOBAL_domain
         AND si_entity = entity
         ,EACH xxpcwob_mstr EXCLUSIVE-LOCK
         WHERE xxpcwob_domain = GLOBAL_domain
         AND xxpcwob_site = si_site
         AND xxpcwob_year = yr
         AND xxpcwob_per = per
         :
         CREATE xxpcwo_mstr.
         ASSIGN
            xxpcwo_domain = global_domain
            xxpcwo_site = xxpcwob_site
            xxpcwo_year = yr
            xxpcwo_per = per
            xxpcwo_part = xxpcwob_part
            xxpcwo_lot = xxpcwob_lot
            xxpcwo_qty_beg = xxpcwob_qty
            .
      END.

      /* 入库和报废 */
      FOR EACH en_mstr NO-LOCK
         WHERE en_domain = GLOBAL_domain
         AND en_entity = entity
         ,EACH si_mstr NO-LOCK
         WHERE si_domain = GLOBAL_domain
         AND si_entity = en_entity
         ,EACH xxpcwor_hist NO-LOCK
         WHERE xxpcwor_domain = GLOBAL_domain
         AND xxpcwor_site = si_site
         AND xxpcwor_year = yr
         AND xxpcwor_per = per
         :
         FIND FIRST xxpcwo_mstr
            WHERE xxpcwo_domain = GLOBAL_domain
            AND xxpcwo_site = xxpcwor_site
            AND xxpcwo_year = yr
            AND xxpcwo_per = per
            AND xxpcwo_part = xxpcwor_part
            AND xxpcwo_lot = xxpcwor_lot
            EXCLUSIVE-LOCK NO-ERROR.
         IF NOT AVAILABLE xxpcwo_mstr THEN DO:
            CREATE xxpcwo_mstr.
            ASSIGN
               xxpcwo_domain = global_domain
               xxpcwo_site = xxpcwor_site
               xxpcwo_year = yr
               xxpcwo_per = per
               xxpcwo_part = xxpcwor_part
               xxpcwo_lot = xxpcwor_lot
               .
         END.

         IF xxpcwor_type = "RCT-WO" THEN DO:
            ASSIGN
               xxpcwo_qty_comp = xxpcwor_qty
               .
         END.
         ELSE IF xxpcwor_type = "RJCT-WO" THEN DO:
            ASSIGN
               xxpcwo_qty_rjct = xxpcwor_qty
               .
         END.
      END.

      /* 领料 */
      FOR EACH en_mstr NO-LOCK
         WHERE en_domain = GLOBAL_domain
         AND en_entity = entity
         ,EACH si_mstr NO-LOCK
         WHERE si_domain = GLOBAL_domain
         AND si_entity = en_entity
         ,EACH xxpcwoi_hist NO-LOCK
         WHERE xxpcwoi_domain = GLOBAL_domain
         AND xxpcwoi_site = si_site
         AND xxpcwoi_year = yr
         AND xxpcwoi_per = per
         :
         FIND FIRST xxpcwo_mstr
            WHERE xxpcwo_domain = GLOBAL_domain
            AND xxpcwo_site = xxpcwoi_site
            AND xxpcwo_year = yr
            AND xxpcwo_per = per
            AND xxpcwo_part = xxpcwoi_par
            AND xxpcwo_lot = xxpcwoi_lot
            EXCLUSIVE-LOCK NO-ERROR.
         IF NOT AVAILABLE xxpcwo_mstr THEN DO:
            CREATE xxpcwo_mstr.
            ASSIGN
               xxpcwo_domain = global_domain
               xxpcwo_site = xxpcwoi_site
               xxpcwo_year = yr
               xxpcwo_per = per
               xxpcwo_part = xxpcwoi_par
               xxpcwo_lot = xxpcwoi_lot
               .
         END.
      END.

      /* SS - 091028.1 - B */
      /* 车间 */
      FOR EACH en_mstr NO-LOCK
         WHERE en_domain = GLOBAL_domain
         AND en_entity = entity
         ,EACH si_mstr NO-LOCK
         WHERE si_domain = GLOBAL_domain
         AND si_entity = en_entity
         ,EACH op_hist NO-LOCK
         WHERE op_domain = GLOBAL_domain
         AND op_site = si_site
         AND op_date >= efdate
         AND op_date <= efdate1
         BREAK
         BY op_site
         BY op_part
         BY op_wo_lot
         :
         IF LAST-OF(op_wo_lot) THEN DO:
            FIND FIRST xxpcwo_mstr
               WHERE xxpcwo_domain = GLOBAL_domain
               AND xxpcwo_site = op_site
               AND xxpcwo_year = yr
               AND xxpcwo_per = per
               AND xxpcwo_part = op_part
               AND xxpcwo_lot = op_wo_lot
               EXCLUSIVE-LOCK NO-ERROR.
            IF NOT AVAILABLE xxpcwo_mstr THEN DO:
               CREATE xxpcwo_mstr.
               ASSIGN
                  xxpcwo_domain = global_domain
                  xxpcwo_site = op_site
                  xxpcwo_year = yr
                  xxpcwo_per = per
                  xxpcwo_part = op_part
                  xxpcwo_lot = op_wo_lot
                  .
            END.
         END. /* IF LAST-OF(op_wo_lot) THEN DO: */
      END. /* FOR EACH en_mstr NO-LOCK */
      /* SS - 091028.1 - E */

      /* 下达与期末 */
      FOR EACH si_mstr NO-LOCK
         WHERE si_domain = GLOBAL_domain
         AND si_entity = entity
         ,EACH xxpcwo_mstr EXCLUSIVE-LOCK
         WHERE xxpcwo_domain = GLOBAL_domain
         AND xxpcwo_site = si_site
         AND xxpcwo_year = yr
         AND xxpcwo_per = per
         :
         
         FIND FIRST wo_mstr
            WHERE wo_domain = global_domain
            AND wo_lot = xxpcwo_lot
            AND wo_part = xxpcwo_part
            NO-LOCK NO-ERROR.
         /* 找不到加工单 */
         IF NOT AVAILABLE wo_mstr THEN DO:
            /* SS - 091028.1 - B
            /* Work Order # ID # not processed */
            {pxmsg.i &MSGNUM=6245 &ERRORLEVEL=3 &MSGARG1=xxpcwo_lot}
            STOP.
            SS - 091028.1 - E */
            /* SS - 091028.1 - B */
            IF allow_errors = NO THEN DO:
               /* Work Order # ID # not processed */
               {pxmsg.i &MSGNUM=6245 &ERRORLEVEL=3 &MSGARG1=xxpcwo_lot}
               STOP.
            END.
            ELSE DO:
               NEXT.
            END.
            /* SS - 091028.1 - E */
         END.

         /* SS - 090807.1 - B */
         /* 已经结算 */
         IF wo_acct_close = YES AND wo_close_eff <= efdate1 THEN DO:
            ASSIGN
               xxpcwo_qty_end = 0
               xxpcwo_qty_ord = xxpcwo_qty_comp + xxpcwo_qty_rjct + xxpcwo_qty_end - xxpcwo_qty_beg
               .
            NEXT.
         END.
         /* SS - 090807.1 - E */

         /* 已经结算 */
         FIND FIRST tr_hist
            WHERE tr_domain = GLOBAL_domain
            AND tr_part = xxpcwo_part
            AND tr_effdate <= efdate1
            AND tr_type = "WO-CLOSE"
            AND ((tr_lot = xxpcwo_lot AND wo_joint_type = "") OR (tr_nbr = wo_nbr AND wo_joint_type <> ""))
            NO-LOCK NO-ERROR.
         IF AVAILABLE tr_hist THEN DO:
            ASSIGN
               xxpcwo_qty_end = 0
               xxpcwo_qty_ord = xxpcwo_qty_comp + xxpcwo_qty_rjct + xxpcwo_qty_end - xxpcwo_qty_beg
               .
            NEXT.
         END. /* IF AVAILABLE tr_hist THEN DO: */

         /* 标准加工单 */
         IF wo_type = "" THEN DO:
            ASSIGN
               xxpcwo_qty_ord = wo_qty_ord - xxpcwo_qty_beg
               xxpcwo_qty_end = xxpcwo_qty_beg + xxpcwo_qty_ord - xxpcwo_qty_comp - xxpcwo_qty_rjct
               .
            IF xxpcwo_qty_end < 0 THEN DO:
               ASSIGN
                  xxpcwo_qty_ord = xxpcwo_qty_ord - xxpcwo_qty_end
                  xxpcwo_qty_end = xxpcwo_qty_end - xxpcwo_qty_end
                  .
            END.

            NEXT.
         END. /* IF wo_type = "" THEN DO: */

         /* 累计加工单 */
         ELSE DO:
            /* 只要存在入库或报废,则期末为0 */
            IF ((xxpcwo_qty_comp <> 0) OR (xxpcwo_qty_rjct <> 0)) THEN DO:
               ASSIGN
                  xxpcwo_qty_end = 0
                  xxpcwo_qty_ord = xxpcwo_qty_comp + xxpcwo_qty_rjct + xxpcwo_qty_end - xxpcwo_qty_beg
                  .
               NEXT.
            END.
            /* 否则期末为1 */
            ELSE DO:
               ASSIGN
                  xxpcwo_qty_end = 1
                  xxpcwo_qty_ord = xxpcwo_qty_comp + xxpcwo_qty_rjct + xxpcwo_qty_end - xxpcwo_qty_beg
                  .
               NEXT.
            END.
         END.
      END.

      /* SS - 091028.1 - B */
      /* 删除数量为0的记录 */
      FOR EACH si_mstr NO-LOCK
         WHERE si_domain = GLOBAL_domain
         AND si_entity = entity
         ,EACH xxpcwo_mstr EXCLUSIVE-LOCK
         WHERE xxpcwo_domain = GLOBAL_domain
         AND xxpcwo_site = si_site
         AND xxpcwo_year = yr
         AND xxpcwo_per = per
         :
         IF xxpcwo_qty_beg = 0 
            AND xxpcwo_qty_ord = 0
            AND xxpcwo_qty_comp = 0
            AND xxpcwo_qty_rjct = 0
            AND xxpcwo_qty_end = 0
            THEN DO:
            DELETE xxpcwo_mstr.
         END.
      END.
      /* SS - 091028.1 - E */
   END. /* DO TRANSACTION ON STOP UNDO: */

   PUT UNFORMATTED "#def REPORTPATH=$/QAD Addons/BI Report/" + SUBSTRING(execname, 1, LENGTH(execname) - 2) SKIP.
   PUT UNFORMATTED "#def :end" SKIP.

   EXPORT DELIMITER ";" 
      "地点"
      "年份"
      "期间"
      "父件"
      "父名"
      "工单"
      "期初"
      "下达"
      "入库"
      "报废"
      "期末"
      .
   FOR EACH en_mstr NO-LOCK
      WHERE en_domain = GLOBAL_domain
      AND en_entity = entity
      ,EACH si_mstr NO-LOCK
      WHERE si_domain = GLOBAL_domain
      AND si_entity = en_entity
      ,EACH xxpcwo_mstr NO-LOCK
      WHERE xxpcwo_domain = GLOBAL_domain
      AND xxpcwo_site = si_site
      AND xxpcwo_year = yr
      AND xxpcwo_per = per
      ,EACH pt_mstr NO-LOCK
      WHERE pt_domain = GLOBAL_domain
      AND pt_part = xxpcwo_part
      BY xxpcwo_site
      BY xxpcwo_year
      BY xxpcwo_per
      BY xxpcwo_part
      BY xxpcwo_lot
      :
      EXPORT DELIMITER ";"
         xxpcwo_site
         xxpcwo_year
         xxpcwo_per
         xxpcwo_part
         (pt_desc1 + " " + pt_desc2)
         xxpcwo_lot
         xxpcwo_qty_beg
         xxpcwo_qty_ord
         xxpcwo_qty_comp
         xxpcwo_qty_rjct
         xxpcwo_qty_end
         .
   END.
   
   {xxmfrtrail.i}

end.

{wbrp04.i &frame-spec = a}
